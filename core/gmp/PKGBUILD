# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
pkgname=gmp
pkgver=4.3.1
pkgrel=2
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i586' 'i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs')
license=('LGPL3')
options=(!libtool)
install=gmp.install
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('26cec15a90885042dd4a15c4003b08ae')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "${CARCH}" = "i686" ] && export ABI="32"
  ./configure --build=$CHOST \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx || return 1
  make || return 1
  make check || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd "${pkgdir}/usr/share/info"
  rm -f dir
  gzip * || return 1
}
