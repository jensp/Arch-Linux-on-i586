# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: James Rayner <iphitus@gmail.com>

pkgname=tiacx
pkgver=20080210
pkgrel=11
pkgdesc="OpenSource module for Texas Instruments ACX100/ACX111 wireless chips. For stock arch 2.6 kernel"
arch=(i586 i686 x86_64)
url="http://acx100.sourceforge.net/"
license=('MPL')
depends=('wireless_tools' 'kernel26>=2.6.31' 'kernel26<2.6.32' 'tiacx-firmware')
install=acx.install
source=(http://downloads.sourceforge.net/sourceforge/acx100/acx-$pkgver.tar.bz2
        kernel-2.6.27.patch
        kernel-2.6.30.patch
        kernel-2.6.31.patch)
_kernver=2.6.31-ARCH

build() {
    cd $startdir/src/acx-$pkgver
    patch -Np1 -i ../kernel-2.6.27.patch || return 1
    patch -Np0 -i ../kernel-2.6.30.patch || return 1
    patch -Np1 -i ../kernel-2.6.31.patch || return 1
    make -C /lib/modules/${_kernver}/build M=`pwd` || return 1
    install -D acx.ko $startdir/pkg/lib/modules/${_kernver}/kernel/drivers/net/wireless/tiacx/acx.ko
    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/*.install
}
md5sums=('7d5ce3215708e4e9f95cf567a9ee3a12'
         '9895f72f8d0c84956b0f6c3b16df0fe8'
         'e2900aee839ce711a7f7606b3e3de36e'
         'dbcb276d77df7801cef58a85c18dfe94')
