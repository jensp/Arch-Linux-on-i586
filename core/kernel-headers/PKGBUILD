# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: kernel-headers->glibc->binutils->gcc->binutils->glibc

pkgname=kernel-headers
pkgver=2.6.31.4
_basever=2.6.31
pkgrel=2
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('i586' 'i686' 'x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL')
groups=('base')
conflicts=("glibc<=2.10.1-4")
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-${_basever}.tar.bz2
	http://www.kernel.org/pub/linux/kernel/v2.6/patch-${pkgver}.bz2
	scsi.patch)
md5sums=('84c077a37684e4cbfa67b18154390d8a'
         '02078f4231baee4f0004212f2875df2b'
         '08204edc45279cfb845208fde9894b9f')

build() {
  cd ${srcdir}/linux-${_basever}
  patch -Np1 -i ${srcdir}/patch-${pkgver} || return 1
  
  # fix issues with transition of scsi.h from glibc
  patch -Np1 -i ${srcdir}/scsi.patch || return 1

  make mrproper
  make headers_check || return 1
  make INSTALL_HDR_PATH=${pkgdir}/usr headers_install

  rm -rf ${pkgdir}/usr/include/drm
}
