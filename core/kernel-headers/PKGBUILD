# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: kernel-headers->glibc->binutils->gcc-libs->gcc->binutils->glibc

pkgname=kernel-headers
pkgver=2.6.30.5
_basever=2.6.30
pkgrel=2
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('i586' 'i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/libc"
groups=('base')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-${_basever}.tar.bz2
        http://www.kernel.org/pub/linux/kernel/v2.6/patch-${pkgver}.bz2)
md5sums=('7a80058a6382e5108cdb5554d1609615'
         '47841c7ff5c81a7b349a79f2fa8e9138')

build() {
  cd "${srcdir}/linux-${_basever}"
  patch -Np1 -i "${srcdir}/patch-${pkgver}" || return 1

  make mrproper || return 1
  make headers_check || return 1
  make INSTALL_HDR_PATH="${pkgdir}/usr" headers_install || return 1

  rm -rf "${pkgdir}/usr/include/drm"
}
