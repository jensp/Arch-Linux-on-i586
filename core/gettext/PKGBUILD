# $Id: PKGBUILD 30624 2009-03-21 03:28:13Z allan $
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=gettext
pkgver=0.17
pkgrel=3
pkgdesc="GNU internationalization library"
arch=(i586 i686 x86_64)
url="http://www.gnu.org/software/gettext"
license=('GPL')
groups=('base')
depends=('gcc-libs' 'acl')
optdepends=('cvs: for autopoint tool')
options=(!libtool)
install=gettext.install
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${pkgname}-${pkgver}.tar.gz)
md5sums=('58a2bc6d39c0ba57823034d55d65d606')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  sed -i -e 's/libexpat.so.0/libexpat.so.1/' gettext-tools/src/x-glade.c
  ./configure --prefix=/usr --enable-csharp
  make || return 1
  make DESTDIR=$pkgdir install
  
  rm -f $pkgdir/usr/share/info/dir
  gzip -9 $pkgdir/usr/share/info/*
}
