# $Id: PKGBUILD 14158 2008-10-04 12:20:01Z tpowa $
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
pkgver=20071127
_traceroutever=1.4a12
pkgrel=2
pkgdesc="IP Configuration Utilities (and Ping)"
arch=(i586 i686 x86_64)
license=('GPL')
url="http://www.linuxfoundation.org/en/Net:Iputils"
groups=('base')
depends=('glibc')
makedepends=('opensp' 'libxslt' 'docbook-xsl')
conflicts=('netkit-base' 'arping' 'netkit-tftpd' 'traceroute')
replaces=('netkit-base' 'traceroute')
source=(http://www.skbuff.net/iputils/$pkgname-s$pkgver.tar.bz2
        ftp://ftp.ee.lbl.gov/traceroute-$_traceroutever.tar.gz
	iputils-20070202-no-open-max.patch
	permission-fix.patch)
options=(!makeflags)
md5sums=('12245e9927d60ff5cf4a99d265bcb7d3'
         '964d599ef696efccdeebe7721cd4828d'
	 'a2cbc0174dd883f68297aa7f3e7c4f5c'
	 '66109966cad06e425bb12a78bab85b70')

build()
{
  #build iptuils
  cd $startdir/src/$pkgname-s$pkgver
  patch -Np1 -i ../iputils-20070202-no-open-max.patch || return 1
  make KERNEL_INCLUDE=/usr/include || return 1   # for i686
  for i in arping clockdiff rarpd rdisc tftpd tracepath tracepath6; do
    install -D -m755 $i $startdir/pkg/usr/sbin/$i
  done
  for i in ping ping6 traceroute6; do
    install -D -m4755 $i $startdir/pkg/bin/$i
  done

  msg ":: building docs"
  # good god, these docs are a pain
  # special thanks to  Michael(tm) Smith <mike@w3.org> - http://people.w3.org/mike/
  # from #docbook on freenode.org for figuring this out
  cd doc
  for file in *.sgml; do
      xf=${file/.sgml/.xml}
      osx -xlower -xno-nl-in-tag $file > $xf
      sed -i "s|<refname>\(.*\), \(.*\)</refname>|<refname>\1</refname>, <refname>\2</refname>|g" $xf
      xsltproc /usr/share/xml/docbook/xsl-stylesheets-1.73.2/manpages/docbook.xsl $xf
  done

  mkdir -p $startdir/pkg/usr/share/man/man8
  install -m644 arping.8 clockdiff.8 ping.8 ping6.8 rarpd.8 rdisc.8 \
                    tftpd.8 tracepath.8 tracepath6.8 traceroute6.8 \
                    $startdir/pkg/usr/share/man/man8/

  # build traceroute
  # this is combined in this package because it's awkward as a separate package
  cd ${startdir}/src/traceroute-$_traceroutever
  patch -Np1 -i ../permission-fix.patch || return 1
  ./configure --prefix=/usr --sbindir=/bin --mandir=/usr/share/man
  make CFLAGS="${CFLAGS} -Ilinux-include/" || return 1
  make DESTDIR=${startdir}/pkg install install-man
}
