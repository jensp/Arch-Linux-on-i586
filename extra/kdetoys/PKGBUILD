# $Id: PKGBUILD 50801 2009-09-01 15:13:29Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdetoys
pkgname=('kdetoys-amor'
         'kdetoys-kteatime'
         'kdetoys-ktux'
         'kdetoys-kweather')
pkgver=4.3.1
pkgrel=1
arch=('i586' 'i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdetoys')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-workspace')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('0cd0bba2f6265751537bf7bdaeacb518')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdetoys-amor() {
	pkgdesc='On-Screen Creature'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/amor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/amor
	make DESTDIR=$pkgdir install
}

package_kdetoys-kteatime() {
	pkgdesc='Tea Cooker'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/kteatime
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kteatime
	make DESTDIR=$pkgdir install
}

package_kdetoys-ktux() {
	pkgdesc='KTux'
	depends=('kdebase-workspace' )
	install='kdetoys.install'
	cd $srcdir/build/ktux
	make DESTDIR=$pkgdir install
}

package_kdetoys-kweather() {
	pkgdesc='A weather reporting panel applet'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdetoys.install'
	cd $srcdir/build/kweather
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kweather
	make DESTDIR=$pkgdir install
}
