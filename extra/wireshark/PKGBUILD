# $Id: PKGBUILD 49495 2009-08-10 00:45:02Z hugo $
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=wireshark
pkgver=1.2.1
pkgrel=1
pkgdesc="A free network protocol analyzer for Unix/Linux and Windows"
arch=('i586' 'i686' 'x86_64')
license=('GPL2')
depends=('gtk2>=2.14.5' 'heimdal>=1.2.1' 'libpcap>=1.0.0' 'bash' 'gnutls>=2.4.1' 'libcap>=2.16')
makedepends=('bison' 'flex')
url="http://www.wireshark.org/"
replaces=('ethereal')
options=(!libtool)
source=(http://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.gz wireshark.desktop wireshark.png
	http://mirror.hamakor.org.il/pub/mirrors/gentoo-portage/net-analyzer/wireshark/files/wireshark-1.1.2--as-needed.patch)
md5sums=('56d760eeec0001b9a0b816ace3e99e44'
         '504ede44c02b2a52f6cbd3bffdf93b8d'
         'e771800a8c977fe223583a453bc27397'
         'cc87d7eb31f256df751b3371b1c426e9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir"/wireshark-1.1.2--as-needed.patch || return 1
  export CFLAGS="-fno-unit-at-a-time ${CFLAGS}"
  autoreconf
  libtoolize
  ./configure --prefix=/usr --with-ssl || return 1
  make all || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -Dm644 ${srcdir}/wireshark.png ${pkgdir}/usr/share/icons/wireshark.png
  install -Dm644 ${srcdir}/wireshark.desktop ${pkgdir}/usr/share/applications/wireshark.desktop
}
