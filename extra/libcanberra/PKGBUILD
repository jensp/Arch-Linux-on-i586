# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libcanberra
pkgver=0.21
pkgrel=1
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification"
arch=(i586 i686 x86_64)
license=('LGPL')
depends=('libvorbis>=1.2.1rc1' 'gstreamer0.10>=0.10.24' 'libtool>=2.2.6a' 'gtk2>=2.18.2' 'alsa-lib>=1.0.20' 'tdb>=3.3.8')
makedepends=('gtk-doc')
install=libcanberra.install
options=(!emptydirs)
url=http://0pointer.de/lennart/projects/libcanberra
source=(http://0pointer.de/lennart/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libcanberra-gtk-module.sh)
md5sums=('6d7b5eb6be8fca491008da529a02f5d5'
         'a54799e624aac814b9343ab05f25c38b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static \
      --enable-alsa --enable-null --enable-gstreamer --disable-oss \
      --with-builtin=dso || return 1
  make || return 1
  make -j1 DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}/usr/lib/libcanberra-gtk.la"
  rm -f "${pkgdir}/usr/lib/gtk-2.0/modules/"*.la
  
  install -m755 -d "${pkgdir}/usr/share/gconf" 
  mv "${pkgdir}/etc/gconf/schemas" "${pkgdir}/usr/share/gconf/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/libcanberra-gtk-module.sh" "${pkgdir}/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module" || return 1
}
