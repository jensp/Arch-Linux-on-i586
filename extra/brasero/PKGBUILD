# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=2.28.2
pkgrel=1
pkgdesc="A disc burning application for Gnome"
arch=('i586' 'i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!libtool' '!emptydirs')
depends=('gstreamer0.10-base-plugins>=0.10.24' 'totem-plparser>=2.28.1' 'libbeagle>=0.3.9' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'libunique>=1.1.2') 
makedepends=('gnome-doc-utils>=0.18.0' 'intltool' 'libburn' 'libisofs' 'nautilus>=2.28.0') 
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'gstreamer0.10-bad-plugins: video project'
            'vcdimager: video project')
install=brasero.install
source=(http://ftp.gnome.org/pub/gnome/sources/brasero/2.28/brasero-${pkgver}.tar.bz2)
sha256sums=('7c3493b1f4e90b4b9a4bcf155c1cd85142bb24a038cc5baa8defdad8526650df')

build() {
  cd "${srcdir}/brasero-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-caches || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain brasero ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
