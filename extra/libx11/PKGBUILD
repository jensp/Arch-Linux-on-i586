# $Id: PKGBUILD 48390 2009-08-01 18:10:18Z jgc $
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor Jan de Groot <jgc@archlinux.org>
pkgname=libx11
pkgver=1.2.2
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i586 i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.4' 'kbproto>=1.0.3')
makedepends=('xproto>=7.0.15' 'xextproto>=7.0.5' 'xf86bigfontproto>=1.1.2' 'xtrans>=1.2.4' 'inputproto>=1.5.1' 'xorg-util-macros>=1.2.2')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh)
md5sums=('94cbee7fae2ddb92b2d80116af871f54'
         '942ff006e69e041f0307c1316e0dec5d')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr \
              --with-xcb || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/xorg.sh" \
                "${pkgdir}/etc/profile.d/" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
