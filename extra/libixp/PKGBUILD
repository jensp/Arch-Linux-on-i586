# $Id: PKGBUILD 27803 2009-02-26 02:11:33Z eric $
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Thanks to Alessio 'mOLOk' Bolognino

pkgname=libixp
pkgver=0.4
pkgrel=1
pkgdesc="A standalone client/server 9p library"
arch=('i586' 'i686' 'x86_64')
url="http://www.suckless.org/libs/libixp.html"
license=('MIT')
depends=('glibc')
options=(!makeflags)
source=(http://code.suckless.org/dl/libs/libixp-$pkgver.tar.gz)
md5sums=('59d9e918adffaf4413b32ac4f66724fd')

build() {
  cd $srcdir/libixp-$pkgver
  sed -i \
        -e "/^PREFIX/s|=.*|= ${startdir}/pkg/usr|" \
        -e "/^  ETC/s|=.*|= ${startdir}/pkg/etc|" \
        -e "/^CFLAGS/s|=|+=|" \
        -e "/^LDFLAGS/s|=|+=|" \
        config.mk || return 1

  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -m644 -D ./libixp/LICENSE \
                   $pkgdir/usr/share/licenses/libixp/LICENSE
}
