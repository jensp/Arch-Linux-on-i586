# $Id: PKGBUILD 50389 2009-08-25 21:31:45Z jgc $
# Maintainer: Alexander Baldeck <kth5@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-server
pkgver=1.6.3
pkgrel=4
pkgdesc="X.Org X servers"
arch=('i586' 'i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
depends=('hal>=0.5.11' 'libgl' 'libxfont>=1.4.0' 'openssl>=0.9.8k' 'libpciaccess>=0.10.6' 'libxv>=1.0.4' 'pixman>=0.14.0' 'xcursor-themes' 'xkeyboard-config>=1.6' 'xorg-server-utils' 'xorg-fonts-misc' 'xbitmaps' 'diffutils' 'xf86-input-evdev>=2.2.3')
makedepends=('libx11>=1.2.2' 'mesa>=7.5' 'xf86driproto>=2.0.4' 'xtrans>=1.2.4' 'libxkbfile>=1.0.5' 'randrproto>=1.3.0' 'renderproto>=0.9.3' 'xcmiscproto>=1.1.2' 'bigreqsproto>=1.0.2' 'resourceproto>=1.0.2' 'videoproto>=2.2.2' 'compositeproto>=0.4' 'scrnsaverproto>=1.1.0' 'xf86dgaproto>=2.0.3' 'recordproto>=1.13.2' 'libgl>=7.5' 'glproto>=1.4.9' 'xorg-util-macros' 'xineramaproto')
conflicts=('catalyst-utils<=9.2')
options=('!libtool')
provides=('x-server')
groups=('xorg')
install=xorg-server.install
source=(${url}/releases/individual/xserver/${pkgname}-${pkgver}.tar.bz2
        xorg-redhat-die-ugly-pattern-die-die-die.patch
        hal-init-failed.patch
        xext-fix-sync-transitions.patch
        xext-bug23100.patch
        dix-dpmsfixes-idletime.patch
        glx-bug21132.patch
        glx-cleanup.patch
        xvfb-run
        xvfb-run.1)
md5sums=('0af168abeefa6579cab20387f75c0c7a'
         '1a336eb22e27cbf443ec5a2ecddfa93c'
         '0f30199da182c531eabd5bbf1c2707e3'
         '2171fbbdb0b4c51a60ad0944351cd910'
         'e9092737cd87aee57013a5d4dcc0e2d4'
         '521980433509b389beb9004b2f5236e4'
         '3f2b2576332754a3dc2c24a10e55caa7'
         '92e17943a7300417d6576dd30eeef180'
         '52fd3effd80d7bc6c1660d4ecf23d31c'
         '376c70308715cd2643f7bff936d9934b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Get rid of the ugly pattern
  patch -Np3 -i "${srcdir}/xorg-redhat-die-ugly-pattern-die-die-die.patch" || return  1

  # Fix crash/abort on hal initialization failure
  patch -Np1 -i "${srcdir}/hal-init-failed.patch" || return 1

  # Fix issue with gnome-power-manager random screen blanking
  patch -Np1 -i "${srcdir}/xext-fix-sync-transitions.patch" || return 1

  # Fixes from http://wiki.x.org/wiki/Server16Branch
  patch -Np1 -i "${srcdir}/xext-bug23100.patch" || return 1
  patch -Np1 -i "${srcdir}/dix-dpmsfixes-idletime.patch" || return 1
  patch -Np1 -i "${srcdir}/glx-bug21132.patch" || return 1
  patch -Np1 -i "${srcdir}/glx-cleanup.patch" || return 1

  # Fix dbus config path
  sed -i -e 's/\$(sysconfdir)/\/etc/' config/Makefile.*  || return 1

  ./configure --prefix=/usr \
              --enable-ipv6 \
              --enable-dri \
              --disable-dmx \
              --enable-xvfb \
              --enable-xnest \
              --enable-composite \
              --enable-xcsecurity \
              --enable-xorg \
      	      --enable-xephyr \
              --enable-glx-tls \
	            --enable-kdrive \
              --enable-install-setuid \
              --enable-config-hal \
      	      --enable-config-dbus \
      	      --enable-record \
      	      --disable-xfbdev \
      	      --disable-xfake \
      	      --disable-xsdl \
              --disable-static \
              --sysconfdir=/etc/X11 \
              --localstatedir=/var \
              --with-default-font-path=/usr/share/fonts/misc,/usr/share/fonts/100dpi:unscaled,/usr/share/fonts/75dpi:unscaled,/usr/share/fonts/TTF,/usr/share/fonts/Type1 \
              --with-xkb-path=/usr/share/X11/xkb \
              --with-xkb-output=/var/lib/xkb \
              --with-dri-driver-path=/usr/lib/xorg/modules/dri || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/" || return 1
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/" || return 1

  rm -rf "${pkgdir}/var/log" || return 1

  install -m755 -d "${pkgdir}/etc/X11" || return 1
  install -m755 -d "${pkgdir}/var/lib/xkb" || return 1

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg" || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
