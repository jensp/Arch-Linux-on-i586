# $Id: PKGBUILD 27437 2009-02-21 19:50:26Z kevin $
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=dansguardian
pkgver=2.10.0.3
pkgrel=1
pkgdesc="Squid plugin for web content filtering."
url="http://dansguardian.org"
arch=('i586' 'i686' 'x86_64')
license=('GPL2')
depends=('squid' 'pcre' 'zlib')
install=$pkgname.install
backup=(etc/dansguardian/dansguardian.conf etc/dansguardian/dansguardianf1.conf)
source=(http://dansguardian.org/downloads/2/Stable/${pkgname}-${pkgver}.tar.gz
        dansguardian
        dansguardian-gcc44-fix.patch)
md5sums=('68c8e9a97a3b58d2467a19cb15db5599'
         'a65a2cb6309670ad8978f1544907d290'
         'f3c94c29e7bb764919d101732dbaf7b0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np0 -i "$srcdir"/dansguardian-gcc44-fix.patch || return 1
  ./configure --prefix="/usr" --with-proxyuser=proxy --with-proxygroup=proxy \
              --sysconfdir=/etc --localstatedir=/var --enable-email
  make || return 1
  make DESTDIR=${pkgdir} install

  install -D data/scripts/dansguardian ${pkgdir}/etc/logrotate.d/dansguardian
  install -D -m755 ${srcdir}/dansguardian ${pkgdir}/etc/rc.d/dansguardian
}
# vim: ts=2 sw=2 et ft=sh
