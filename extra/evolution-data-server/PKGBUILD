# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=2.28.0
pkgrel=1
pkgdesc="Evolution Data Server provides a central location for addressbook and calendar in the GNOME Desktop"
arch=(i586 i686 x86_64)
depends=('libsoup>=2.27.92' 'nss>=3.12.4' 'libglade>=2.6.4' 'gnome-keyring>=2.27.92' 'db4.1' 'heimdal>=1.2.1' 'libgweather>=2.27.91' 'libical>=0.43' 'libbonobo>=2.24.1')
makedepends=('intltool' 'pkgconfig' 'gtk-doc')
options=('!libtool')
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('01fead604816fb135aeb5921687d99773e966014a17c1cae230c307386e13814')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Look for correct DB, we want 4.1
  sed -e 's|DB_CFLAGS="-I$withval/include"|DB_CFLAGS="-I$withval/include/db4.1"|' \
      -e 's|DB_LIBS="-L$withval/lib -ldb"|DB_LIBS="-L$withval/lib -ldb-4.1"|' \
      -i configure || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server-1.2 \
      --with-krb5=/usr --enable-gnome-keyring \
      --with-libdb=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
