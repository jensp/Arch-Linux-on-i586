# $Id: PKGBUILD 50027 2009-08-18 14:51:29Z jgc $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=freeglut
pkgver=2.4.0
pkgrel=4
pkgdesc="Provides functionality for small OpenGL programs"
arch=('i586' 'i686' 'x86_64')
url="http://freeglut.sourceforge.net/"
license=('custom')
depends=('libxxf86vm' 'mesa')
replaces=('glut')
provides=('glut')
conflicts=('glut')
options=('!libtool')
source=(http://downloads.sourceforge.net/freeglut/${pkgname}-${pkgver}.tar.gz \
        glut-cursor-inherit.patch)
md5sums=('6d16873bd876fbf4980a927cfbc496a1'
         'd6ba7e22ebb0fd7904b8e18c97475ba6')
sha1sums=('91a528aa72758b7288a0d69a964b1b7e3f322a12'
          '9353d7b811fdfd6f0c5766f0e93924ca63e9d6a9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/glut-cursor-inherit.patch" || return 1
  sed -i 's/-Werror//' configure || return 1
  ./configure --prefix=/usr || return 1
  make all || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
