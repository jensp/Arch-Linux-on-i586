# $Id: PKGBUILD 29966 2009-03-15 02:54:25Z hugo $
# Maintainer: damir <damir@archlinux.org>

pkgname=muse
pkgver=0.9
pkgrel=3
pkgdesc="MusE is a MIDI/Audio sequencer with recording and editing capabilities. MusE aims to be a complete multitrack virtual studio for Linux."
arch=(i586 i686 x86_64)
url="http://muse-sequencer.org/"
license=('GPL')
depends=('jack-audio-connection-kit' 'gcc-libs' 'qt3' 'glibc' 'fluidsynth' 'graphviz' 'libsamplerate')
makedepends=('jade')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/lmuse/$pkgname-$pkgver.tar.gz gcc43_build_fixes.patch
	muse-gcc44.patch)
md5sums=('04fbefa4a5eba62cb72d6d7035a0601b'
         '2188bcd64781e51256645837f86a3330'
         '05a8f11c012b96136d27ede174bed59f')

build() {
  cd $srcdir/$pkgname-$pkgver
  unset LDFLAGS
  . /etc/profile.d/qt3.sh
  patch -Np1 -i ../gcc43_build_fixes.patch || return 1
  patch -Np0 -i "$srcdir"/muse-gcc44.patch || return 1
  ./configure --prefix=/usr --enable-maintainer-mode \
                            --disable-doxy-treeview \
                            --enable-optimize \
                            --enable-vst \
                            --disable-suid-build || return 1
  make || return 1
  make prefix=$pkgdir/usr install || return 1
}
