# $Id: PKGBUILD 50790 2009-09-01 15:07:46Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeadmin
pkgname=('kdeadmin-kcron'
         'kdeadmin-ksystemlog'
         'kdeadmin-kuser'
         'kdeadmin-system-config-printer-kde')
pkgver=4.3.1
pkgrel=1
arch=('i586' 'i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'kdebase-runtime' 'kdebindings-python' 'system-config-printer>=1.1.7-3')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
	'fix-syslog-path.patch')
md5sums=('e946d243d0e6e1fdbb24c732acca9c1e'
         '41b71823c7a33975ec08aaf75d67a374')

build() {
	cd $srcdir/${pkgbase}-${pkgver}
	patch -p1 -i ${srcdir}/fix-syslog-path.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_kpackage=OFF \
		-DBUILD_knetworkconf=OFF \
		-DBUILD_lilo-config=OFF \
		-DBUILD_strigi-analyzer=OFF
	make
}

package_kdeadmin-kcron() {
	pkgdesc='Configure and schedule tasks'
	depends=('kdelibs')
	cd $srcdir/build/kcron
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcron
	make DESTDIR=$pkgdir install
}

package_kdeadmin-ksystemlog() {
	pkgdesc='System log viewer tool'
	depends=('kdelibs' 'kdebase-runtime')
	cd $srcdir/build/ksystemlog
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ksystemlog
	make DESTDIR=$pkgdir install
}

package_kdeadmin-kuser() {
	pkgdesc='User Manager'
	depends=('kdepimlibs' 'kdebase-runtime')
	install='kdeadmin.install'
	cd $srcdir/build/kuser
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kuser
	make DESTDIR=$pkgdir install
}

package_kdeadmin-system-config-printer-kde() {
	pkgdesc='Configure local and remote Printers'
	depends=('kdebindings-python' 'system-config-printer>=1.1.7-3' )
	cd $srcdir/build/system-config-printer-kde
	make DESTDIR=$pkgdir install
}
