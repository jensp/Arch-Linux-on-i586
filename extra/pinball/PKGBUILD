# $Id: PKGBUILD 35775 2009-04-17 03:29:19Z eric $
# Maintainer: dale <dale@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=pinball
pkgver=0.3.1
pkgrel=4
pkgdesc="The Emilia Pinball Project strives to fulfil your needs for a great pinball game in Linux"
arch=('i586' 'i686' 'x86_64')
url="http://pinball.sourceforge.net/"
license=('GPL2')
depends=('sdl' 'sdl_mixer' 'sdl_image' 'mesa')
options=('!makeflags' 'libtool')
install=pinball.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	http://prefix.gentooexperimental.org/rsync-gentoo-x86/games-arcade/emilia-pinball/files/emilia-pinball-0.3.1-glibc210.patch)
md5sums=('f28e8f49e0db8e9491e4d9f0c13c36c6'
         'b10c43cf7b311c20fd12f2b8d299b74e')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np0 -i "$srcdir"/emilia-$pkgname-$pkgver-glibc210.patch
  ./configure --prefix=/usr --disable-sdltest || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  chown root:games $pkgdir/usr/bin/pinball
  chmod 2755 $pkgdir/usr/bin/pinball
  chown root:games $pkgdir/var/games
  chmod 775 $pkgdir/var/games
  chown root:games $pkgdir/var/games/pinball/{professor,tux}
  chmod 775 $pkgdir/var/games/pinball/{professor,tux}
  rm $pkgdir/var/games/pinball/*/highscores
}
