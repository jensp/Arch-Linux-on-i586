# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=2.28.1
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://www.gnome.org"
arch=(i586 i686 x86_64)
license=('GPL')
depends=('libspectre>=0.2.3' 'gsfonts' 'poppler-glib>=0.12.1' 'libdjvu>=3.5.22' 'gnome-icon-theme>=2.28.0' 't1lib' 'gnome-keyring>=2.28.1' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.18.0' 'nautilus>=2.28.0' 'pkgconfig' 'texlive-bin' 'intltool')
optdepends=('texlive-bin: DVI support')
replaces=('gpdf')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c64047d4dc8424b613274651a5ef6bc78d306fc71bef36a3e033c07ddc2487a6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-nautilus \
      --enable-pdf --enable-tiff \
      --enable-djvu --enable-dvi \
      --enable-t1lib --enable-pixbuf \
      --enable-comics --enable-impress \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evince ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
