# $Id: PKGBUILD 49696 2009-08-15 10:39:08Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxml2
pkgver=2.7.3
pkgrel=3
pkgdesc="XML parsing library, version 2"
arch=(i586 i686 x86_64)
license=('custom')
depends=('zlib>=1.2.3.3' 'readline>=6.0' 'ncurses>=5.7')
makedepends=('python')
options=('!libtool')
url="http://www.xmlsoft.org/"
source=(ftp://ftp.xmlsoft.org/${pkgname}/${pkgname}-${pkgver}.tar.gz
        largefile64.patch
        libxml2-2.7.3-ficora-parse.patch)
md5sums=('8f4fda3969237c2a33bdb1583b5d06b2'
         '5ad4915665608ebfa5b89f7908467a72'
         'e4ed4ef08a941d44d053f5418945fc5d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/largefile64.patch" || return 1
  patch -Np1 -i "${srcdir}/libxml2-2.7.3-ficora-parse.patch" || return 1
  ./configure --prefix=/usr --with-threads --with-history || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
