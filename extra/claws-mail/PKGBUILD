# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=claws-mail
pkgver=3.7.3
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i586' 'i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2>=2.16.2' 'gnutls>=2.8.1' 'startup-notification>=0.10' 'pilot-link>=0.12.3-5' 'enchant>=1.4.2-1'
         'gpgme>=1.1.8' 'libetpan>=0.58' 'libsm>=1.1.0' 'db>=4.8' 'curl>=7.19.5-2' 'dbus-glib>=0.80' 'hicolor-icon-theme')
makedepends=('compface' 'spamassassin' 'bogofilter' 'valgrind')
optdepends=('python:            needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering')
replaces=('sylpheed-claws')
provides=('claws')
options=(!libtool)
install=claws-mail.install
source=(http://downloads.sourceforge.net/sourceforge/sylpheed-claws/${pkgname}-${pkgver}.tar.bz2)
md5sums=('b3af5ea5e1182aa53f971657db86e110')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --disable-dillo-viewer-plugin \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin \
    --enable-jpilot

  make || return 1
  make DESTDIR=${pkgdir} install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 ${i} \
        ${pkgdir}/usr/lib/claws-mail/tools/${i}
  done
}
