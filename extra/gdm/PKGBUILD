# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=gdm
pkgver=2.28.0
pkgrel=1
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=(i586 i686 x86_64)
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm' 'etc/pam.d/gdm-autologin' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra>=0.14' 'gnome-panel>=2.28.0' 'libxklavier>=4.0' 'tcp_wrappers' 'gnome-session>=2.28.0' 'devicekit-power>=010')
makedepends=('intltool' 'gnome-doc-utils>=0.18.0' 'xorg-server')
conflicts=('fast-user-switch-applet')
replaces=('fast-user-switch-applet')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2
        04_fix_external_program_directories.patch
        gdm-2.28.0-use-devicekit-power.patch
        gdm.pam
        gdm-autologin.pam
        gdm)
sha256sums=('b83db2613192df8a8865ca59877de5181df6247cd17be42418cc5a39d314d5a4'
            'd41b0945a3db78a06b5aa4101f5515f7ea5d57e70974681c23e5def1df1834bb'
            '343f82a570aec7cdfbeb559c8dfd206677e74bd93bf01a24cddce144950799c5'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/04_fix_external_program_directories.patch" || return 1
  patch -Np1 -i "${srcdir}/gdm-2.28.0-use-devicekit-power.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm --localstatedir=/var --disable-static \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -r "${pkgdir}/etc/gconf/schemas/"

  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm" || return 1
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin" || return 1

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/" || return 1

  rmdir "${pkgdir}/var/gdm"
}
