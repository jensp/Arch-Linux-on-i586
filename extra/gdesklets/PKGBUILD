# $Id: PKGBUILD 20772 2008-12-06 17:26:06Z allan $
# Committer: nifan <carlos@nifan.tk>
# Mantainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gdesklets
pkgver=0.36.1
pkgrel=1
pkgdesc="gDesklets provides an advanced architecture for desktop applets."
arch=('i586' 'i686' 'x86_64')
license=('GPL')
url="http://gdesklets.de/"
depends=('libgtop>=2.14.8-2' 'librsvg>=2.16.0' 'libgnomeui>=2.18.1-2'
         'gnome-python>=2.18.0' 'gconf>=2.18.0.1-4' 'desktop-file-utils')
makedepends=('perlxml' 'swig')
install=gdesklets.install
source=(http://gdesklets.de/files/gdesklets-$pkgver.tar.gz
        no-import-overide.patch)
md5sums=('973d463ac6f0037801495de09ebc495a'
         '9190f9c8406e3c5ad5c0ab2bd95b71ff')
options=('!libtool')

build() {
  cd $srcdir/gDesklets-$pkgver
  patch -Np1 -i $srcdir/no-import-overide.patch
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1

  make UPDATE_MIME_DATABASE=/bin/true UPDATE_DESKTOP_DATABASE=/bin/true \
    GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install

  mkdir -p $pkgdir/usr/share/gconf/schemas
  gconf-merge-schema $pkgdir/usr/share/gconf/schemas/$pkgname.schemas \
    $pkgdir/etc/gconf/schemas/*.schemas
  rm -f $pkgdir/etc/gconf/schemas/*.schemas
}
