# $Id: PKGBUILD 30847 2009-03-22 07:10:20Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=ddd
pkgver=3.3.12
pkgrel=1
pkgdesc="A graphical front-end for command-line debuggers such as GDB, JDB, pydb, perl debugger..."
arch=('i586' 'i686' 'x86_64')
url="http://www.gnu.org/software/ddd/"
license=('GPL3' 'LGPL3')
depends=('gcc-libs' 'ncurses' 'lesstif' 'libxaw>=1.0.5' 'libelf' 'libxext')
optdepends=('gdb: to use the Gnu debugger' 'jdk: to use the Java debugger' 'pydb: to use the Python debugger' 'perl: to use the Perl debugger')
install=$pkgname.install
source=(http://ftp.gnu.org/gnu/ddd/${pkgname}-${pkgver}.tar.gz
	http://ftp.riken.go.jp/pub/Linux/gentoo/dev-util/ddd/files/ddd-3.3.12-gcc44.patch)
md5sums=('c50396db7bac3862a6d2555b3b22c34e'
         '41dc59d8cf502569bab1b1a4a222a1c6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir"/ddd-3.3.12-gcc44.patch || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 icons/ddd.xpm ${pkgdir}/usr/share/pixmaps/ddd.xpm || return 1
  rm ${pkgdir}/usr/share/info/dir || return 1
  gzip ${pkgdir}/usr/share/info/* || return 1
}
