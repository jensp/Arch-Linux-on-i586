# $Id: PKGBUILD 29405 2009-03-08 15:50:59Z tpowa $
# Contributor: Camille Moncelier <pix@devlife.org>, simo <simo@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kexec-tools
pkgver=2.0.0
pkgrel=1
pkgdesc="Load another kernel from the currently executing Linux kernel"
arch=(i586 i686 x86_64)
url="http://www.kernel.org/pub/linux/kernel/people/horms/kexec-tools/"
source=(http://www.kernel.org/pub/linux/kernel/people/horms/kexec-tools/kexec-tools-2.0.0.tar.bz2
        kexec kexec.conf.d
	http://213.186.33.38/gentoo-portage/sys-apps/kexec-tools/files/kexec-tools-2.0.0-asneeded.patch
	http://213.186.33.38/gentoo-portage/sys-apps/kexec-tools/files/kexec-tools-2.0.0-respect-LDFLAGS.patch)
depends=('zlib' 'awk')
backup=('etc/conf.d/kexec')
license=('GPL2')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np0 -i "$srcdir"/kexec-tools-2.0.0-asneeded.patch || return 1
  patch -Np0 -i "$srcdir"/kexec-tools-2.0.0-respect-LDFLAGS.patch || return 1
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  
  mv $pkgdir/usr/sbin $pkgdir/sbin

  install -D -m644 kexec/kexec.8 $pkgdir/usr/share/man/man8/kexec.8
  install -D -m644 $srcdir/kexec.conf.d $pkgdir/etc/conf.d/kexec
  install -D -m755 $srcdir/kexec $pkgdir/etc/rc.d/kexec
}
md5sums=('d9f2ecd3c3307905f24130a25816e6cc'
         '15599234f174ddc4d2611f32effec6ab'
         'b9ddfb9fbcc7e4e7e7294fe94fa60aeb'
         '618c79fe17c05d51069d02e3aba7474a'
         'a2a5e201ae5d655208c5815b17015b6b')

