# $Id: PKGBUILD 43694 2009-06-29 18:04:41Z thomas $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=bash-completion
pkgver=1.0
pkgrel=2
pkgdesc="Programmable completion for the bash shell"
arch=(i586 i686 x86_64)
url="http://bash-completion.alioth.debian.org/"
license=('GPL')
depends=('bash')
source=(http://bash-completion.alioth.debian.org/files/$pkgname-$pkgver.tar.gz
        bash_completion.sh
        fix-_filedir-on-bash-4.patch)
md5sums=('cd1c5648272917fbe0eef4ba30bb93f4'
         'b02622e759e01b2077617ddfc51cbe43'
         '1c707436109fb4c80ef7abeefc7dd1b0')
options=('force')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fixes for archlinux
  sed -i 's#/sbin/lsmod#/bin/lsmod#' bash_completion || return 1

  patch -up1 < $srcdir/fix-_filedir-on-bash-4.patch

  ./configure --prefix=/usr --sysconfdir=/etc
  make || return 1
  make DESTDIR="$pkgdir" install

  # apache2ctl doesn't work, even when renamed to apachectl (and sed'd)
  rm "$pkgdir/etc/bash_completion.d/apache2ctl"

  install -Dm755 "$srcdir/bash_completion.sh" "$pkgdir/etc/profile.d/bash_completion.sh"
}
# vim:set ts=2 sw=2 et:
