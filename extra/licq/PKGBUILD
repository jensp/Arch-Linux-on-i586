# $Id: PKGBUILD 32359 2009-03-30 17:59:43Z pierre $
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=licq
pkgver=1.3.6
pkgrel=2
pkgdesc="Advanced graphical ICQ clone and more for Unix" 
arch=(i686 x86_64)
url="http://www.licq.org"
license=('GPL')
depends=('libxss' 'qt' 'openssl' 'boost')
options=(!libtool)
source=(http://downloads.sourceforge.net/sourceforge/licq/licq-${pkgver}.tar.bz2
	licq-1.3.6-glibc-2.10.patch)
md5sums=('9a2842dceba565601d0339347e6e203d'
	 'ab7ee2b6c7cc4e21624d3427e37205d0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../licq-1.3.6-glibc-2.10.patch || return 1
  # licq
  GPGME_CONFIG=/bin/false ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # qt4-gui
  cd plugins/qt4-gui || return 1
  mkdir build
  cd build

  cmake -DWITH_KDE=OFF -DCMAKE_INSTALL_PREFIX=/usr .. || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
