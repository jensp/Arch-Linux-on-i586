# $Id: PKGBUILD 43576 2009-06-25 14:51:58Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=fuse
pkgver=2.7.4
pkgrel=3
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i586' 'i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license="GPL2"
depends=('glibc')
makedepends=('pkgconfig')
backup=(etc/fuse.conf etc/udev/rules.d/99-fuse.rules)
install=fuse.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	'fuse.conf')
options=(!libtool)
md5sums=('4879f06570d2225667534c37fea04213'
         'c9457cf5b2196da67d5ac816d1c86a4f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-kernel-module --enable-lib \
              --enable-util --bindir=/bin
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # static device nodes will cause collision.
  rm -rf ${pkgdir}/dev
 
  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf ${pkgdir}/etc/init.d
  #install -D -m755 ${srcdir}/fuse.rc.d ${pkgdir}/etc/rc.d/fuse

  install -d ${pkgdir}/lib
  mv ${pkgdir}/usr/lib/libfuse.so.${pkgver} ${pkgdir}/lib/ || return 1
  ln -s ../../lib/libfuse.so.${pkgver} ${pkgdir}/usr/lib/libfuse.so.${pkgver} || return 1
  ln -s libfuse.so.${pkgver} ${pkgdir}/lib/libfuse.so.2 || return 1
  mv ${pkgdir}/usr/lib/libulockmgr.so.1.0.1 ${pkgdir}/lib/ || return 1
  ln -s ../../lib/libulockmgr.so.1.0.1 ${pkgdir}/usr/lib/libulockmgr.so.1.0.1 || return 1
  ln -s libulockmgr.so.1.0.1 ${pkgdir}/lib/libulockmgr.so.1 || return 1

  # install sample config file
  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf
}

