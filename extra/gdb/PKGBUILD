# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=7.0
pkgrel=2
pkgdesc="The GNU Debugger"
arch=('i586' 'i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python')
makedepends=('texinfo')
options=('!libtool')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.bz2
        gdb-readline-6.0.patch)
md5sums=('3386a7b69c010785c920ffc1e9cb890a'
         '82e9eac057954899092405e9ef97d029')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/gdb-readline-6.0.patch
  
  ./configure --prefix=/usr --disable-nls --with-system-readline
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # resolve conflicts with binutils
  rm -f ${pkgdir}/usr/include/{bfd,dis-asm,symcat,ansidecl,bfdlink}.h
  rm -f ${pkgdir}/usr/lib/{libiberty,libbfd,libopcodes}.a
  rm -f ${pkgdir}/usr/share/info/{bfd.info,configure.info,standards.info}
}
