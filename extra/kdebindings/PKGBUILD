# $Id: PKGBUILD 50783 2009-09-01 15:04:40Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebindings
pkgname=('kdebindings-csharp'
         'kdebindings-python'
         'kdebindings-ruby'
         'kdebindings-smoke')
groups=('kdebindings')
pkgver=4.3.1
pkgrel=1
arch=('i586' 'i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'pyqt>=4.5.0' 'polkit-qt' 'qimageblitz' 'qscintilla' 'ruby' 'mono' 'kdegraphics-okular')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('6cc71c3f6136d3b69f7a109d03ddfe40')

build() {
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build

	# disable broken examples
	sed -e 's#add_subdirectory( examples )##' -i ../${pkgbase}-${pkgver}/csharp/plasma/CMakeLists.txt || return 1
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_java=OFF \
		-DBUILD_php=OFF \
		-DBUILD_falcon=OFF
	make
}

package_kdebindings-csharp() {
	pkgdesc='KDE bindings for mono'
	depends=('kdebindings-smoke' 'mono')
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build/csharp
	make DESTDIR=$pkgdir install
}

package_kdebindings-python() {
	pkgdesc='KDE bindings for python'
	depends=('kdepimlibs' 'pyqt' 'polkit-qt')
	cd $srcdir/build/python
	make DESTDIR=$pkgdir install
}

package_kdebindings-ruby() {
	pkgdesc='KDE bindings for ruby'
	depends=('kdebindings-smoke' 'ruby')
	cd $srcdir/build/ruby
	make DESTDIR=$pkgdir install
}

package_kdebindings-smoke() {
	pkgdesc='Language independent library for Qt and KDE bindings'
	depends=('kdepimlibs' 'qscintilla')
	groups=()
	cd $srcdir/build/smoke
	make DESTDIR=$pkgdir install
}
