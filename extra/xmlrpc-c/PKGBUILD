# Maintainer: Jens Pranaitis <jens@chaox.net>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Vitaliy Berdinskikh <skipper13@root.ua>

pkgname=xmlrpc-c
pkgver=1.06.36
pkgrel=1
pkgdesc='This library provides a modular implementation of XML-RPC for C and C++'
arch=('i586' 'i686' 'x86_64')
url='http://xmlrpc-c.sourceforge.net/'
license=('custom')
depends=('curl' 'libxml2')
makedepends=('libtool')
options=('!makeflags' '!libtool')
source=("http://downloads.sourceforge.net/xmlrpc-c/xmlrpc-c-${pkgver}.tgz")
md5sums=('22b322ea97bfd54f5bc1caaf02f995a9')

build() {
	[ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"

	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr \
		--enable-libxml2-backend \
		--disable-cgi-server \
		--disable-abyss-server \
		--disable-libwww-client \
		--disable-wininet-client

	make CFLAGS_PERSONAL="${CFLAGS}" || return 1
	make DESTDIR=$pkgdir install

	# remove bad symlink
	rm $pkgdir/usr/include/xmlrpc_cgi.h

	install -m 644 -D doc/COPYING $pkgdir/usr/share/licenses/${pkgname}/COPYING
}
