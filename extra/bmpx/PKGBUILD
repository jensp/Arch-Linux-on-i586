# $Id: PKGBUILD 703 2008-04-22 00:13:48Z eric $
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: alzen <mynick2(at)gmail(dot)com>

pkgname=bmpx
pkgver=0.40.14
pkgrel=1
pkgdesc="A media player and the successor to BMP"
arch=(i586 i686 x86_64)
url="http://bmpx.backtrace.info/site/BMPx_Homepage"
depends=('boost' 'fam'
	'gstreamer0.10-good' 'hal' 'libglademm' 'libofa'
	'librsvg' 'libsexymm' 'libsoup>=2.4.0' 'sqlite3' 'startup-notification>=0.8'
	'taglib')
# 0.40.0: bmpx provide their own neon and musicbrainz implementation.
makedepends=('bison' 'flex' 'pkgconfig' 'perlxml')
options=('!libtool' '!makeflags')
license=('GPL' 'custom')
install=bmpx.install
source=(http://files.backtrace.info/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
	bmpx-libsoup24.patch
	bmpx-compilation.patch)
md5sums=('c741e05a82a82b14b6775d44a7c93c15'
         'ef433f8b306b5d9e7d3f71dade83b751'
         'b178da5c03cabedf403060e8c992cba9')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir"/bmpx-libsoup24.patch || return 1
  patch -Np1 -i "$srcdir"/bmpx-compilation.patch || return 1
  ./configure --prefix=/usr --enable-amazon --libexecdir=/usr/lib/bmpx
  sed -e '/BUILD_BUILDUSER/ s@".*"@"http://www.archlinux.org"@' -i build.h
  make || return 1
  make DESTDIR=${startdir}/pkg install

  # install license
  install -D -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/LICENSE

  #Fix invalid symlink pointing to builddir
  cd ${startdir}/pkg/usr/bin
  rm -f bmp2
  ln -sf beep-media-player-2 bmp2
}
