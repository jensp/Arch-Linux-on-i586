# $Id: PKGBUILD 15814 2008-10-19 10:04:49Z douglas $
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
pkgname=openldap-clients
pkgver=2.4.17
pkgrel=1
pkgdesc="LDAP Client utilities"
arch=(i586 i686 x86_64)
license=('custom')
url="http://www.openldap.org/"
depends=("libldap>=${pkgver}")
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz
        openldap-2.3.41-r1-fixed-peercred-error.patch)
options=('!makeflags')

md5sums=('5e82103780f8cfc2b2fbd0f77c47c158'
         'a68214b19bee704992d80322cac3ce49')

build() {
  cd ${startdir}/src/openldap-${pkgver}
  patch -p1 < ../openldap-2.3.41-r1-fixed-peercred-error.patch 
  ./configure --prefix=/usr \
              --libexecdir=/usr/sbin \
              --sysconfdir=/etc \
              --localstatedir=/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --enable-wrappers \
	      --enable-spasswd --with-cyrus-sasl \
	      --disable-bdb --disable-hdb
  
  find . -name 'Makefile' -exec \
  	sed -e 's|$(LDAP_LIBDIR)/liblber/liblber.la|/usr/lib/liblber-2.4.so.2|g' \
	    -e 's|$(LDAP_LIBDIR)/libldap/libldap.la|/usr/lib/libldap-2.4.so.2|g' \
	    -e 's|$(LDAP_LIBDIR)/libldap_r/libldap_r.la|/usr/lib/libldap_r-2.4.so.2|g' \
	    -i {} \;

  cd include
  make || return 1

  cd ../libraries
  for dir in liblutil librewrite liblunicode; do
    pushd ${dir}
    make depend || return 1
    make || return 1
    popd
  done
  cd ../clients
  make depend || return 1
  make || return 1

  make DESTDIR=${startdir}/pkg install

  cd ../doc/man/man1
  make || return 1
  make DESTDIR=${startdir}/pkg install
}
